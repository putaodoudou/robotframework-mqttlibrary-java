| *Settings* | *Value*
| Library    | MQTTLibrary
| Library    | BuiltIn
     
| *Variables*   | *Value*
| ${broker.uri} | tcp://127.0.0.1:1883
| ${client.id}  | test.client
| ${topic}      | test.topic

| *Test Case*   | *Action*                      | *Argument*    | *Argument*    | *Argument*
| Publish a non-empty message
|               | ${time}                           | Get Time      | epoch
|               | ${message}                        | Catenate      | test message  | ${time}
|               | Publish to MQTT and Disconnect    | ${broker.uri} | ${client.id}  |
|               | ...                               | ${topic}      | ${message}    |
|               | Log                               | ${message}    |

| Publish an empty message
|               | Publish to MQTT and Disconnect    | ${broker.uri} | ${client.id}  |
|               | ...                               | ${topic}      | ${EMPTY}      |

| Subscribe and validate that a message is received
|               | [Timeout]                                 | 30
|               | ${time}                                   | Get Time      | epoch |
|               | ${sub.client}                             | Catenate      | SEPARATOR=.   | robot.mqtt | ${time}
|               | ${message}                                | Set Variable  | subscription test message |
|               |  Run Keyword And Expect Error             | The expected payload didn't arrive in the topic
|               | ...   | Subscribe to MQTT and Validate    | ${broker.uri} | ${sub.client} | sub.topic | ${message}    | 1000
|               | Connect to MQTT Broker                    | ${broker.uri} | ${client.id}
|               | Publish to MQTT Synchronously             | sub.topic     | ${message}    | 1     | false
|               | Disconnect from MQTT Broker               |
|               | Subscribe to MQTT and Validate            | ${broker.uri} | ${sub.client} | sub.topic | ${message}    | 1000



| *Keywords*    |
| Publish to MQTT and Disconnect    | [Documentation]   | Publish a message to MQTT queue and disconnect
|                                   | [Arguments]       | ${broker.uri} | ${client.id}
|                                   | ...               | ${topic}      | ${message}
|               | Connect to MQTT Broker        | ${broker.uri} | ${client.id}
|               | Publish to MQTT Synchronously | ${topic}      | ${message}
|               | Disconnect from MQTT Broker   |
